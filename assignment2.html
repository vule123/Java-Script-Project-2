<!DOCTYPE html>
<html>

<body onload="init()" onkeydown="keyPressed(event)">
    <canvas id="glcanvas" width="960" height="540">
        Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
    </canvas>

    <script id="vertex-shader" type="x-shader/x-vertex">
        precision mediump float;
        precision mediump int;

        // position and normal of the vertices
        attribute vec4 vPosition;
        attribute vec4 vNormal;
        attribute vec4 vFlatPosition;

        uniform mat4 modelViewMatrix;
        uniform mat3 normalMatrix;
        uniform mat4 perspectiveProjection;
        uniform mat4 orthographicProjection;

        uniform bool isPerspective;

        uniform vec4 lightPosition;
        uniform int  shadingType;
        uniform vec3 ambientProduct;
        uniform vec3 diffuseProduct;
        uniform vec3 specularProduct;
        uniform float shininess;

        // the color for flat and Gouraud shading
        varying vec4 vColor;
        // the normal and view vector for Phong shading
        varying vec3 vNorm, vLight, vEye;

        varying float vDist;

        void main() {
        float a = 0.0001, b = 0.01, c = 1.0;

        vec4 eyePosition = modelViewMatrix * vPosition;

        vDist = distance(lightPosition, eyePosition);
        // components for Gouraud shading
        vec3 ambient, diffuse, specular;

        ambient = ambientProduct;

        // transfer the normal, incident and eye vector to the fragment shader: Phong
        vNorm = normalize(normalMatrix * vNormal.xyz);
        // for flat shading, calculate light direction and view vector for vertices
        if (shadingType == 0) 
            vLight = normalize(lightPosition - modelViewMatrix * vFlatPosition).xyz;
        else 
            vLight = normalize(lightPosition - eyePosition).xyz;
        diffuse = max(dot(vLight, vNorm), 0.0) * diffuseProduct;

        if (shadingType == 0) 
            vEye = -normalize((modelViewMatrix * vFlatPosition).xyz);
        else 
            vEye = -normalize(eyePosition.xyz);
        vec3 V = normalize(vLight + vEye);
        specular = pow(max(dot(vNorm, V), 0.0), shininess) * specularProduct;
        if (dot(vLight, vNorm) < 0.0) 
            specular = vec3(0.0, 0.0, 0.0);

        // transfer the color of this vertice to the fragment shader: Gouraud or flat
        vColor = vec4(ambient + (diffuse + specular) / (a * vDist * vDist + b * vDist + c), 1);

        if (isPerspective) 
            gl_Position = perspectiveProjection * eyePosition;
        else 
            gl_Position = orthographicProjection * eyePosition;
        
    }

    </script>

    <script id="fragment-shader" type="x-shader/x-fragment">
        // receive the color of pixels from the vertex shader
        precision mediump float;
        precision mediump int;

        uniform int  shadingType;
        uniform vec3 ambientProduct;
        uniform vec3 diffuseProduct;
        uniform vec3 specularProduct;
        uniform float shininess;

        varying vec4 vColor;
        varying vec3 vNorm, vLight, vEye;

        varying float vDist;

        void main() {
        // generate the color based on the shading type
        if (shadingType < 2) {
            // Gouraud or flat
            gl_FragColor = vColor;
        }
        else {
            // Phong
            float a = 0.0001, b = 0.01, c = 1.0;

            vec3 V = normalize(vLight + vEye);
            vec3 ambient = ambientProduct;

            vec3 diffuse = max(dot(vLight, vNorm), 0.0) * diffuseProduct;

            vec3 specular = pow(max(dot(vNorm, V), 0.0), shininess) * specularProduct;
            if (dot(vLight, vNorm) < 0.0) 
                specular = vec3(0.0, 0.0, 0.0);
        
            gl_FragColor = vec4(ambient + (diffuse + specular) / (a * vDist * vDist + b * vDist + c), 1);
        }
    }
    </script>
    <script type="text/javascript" src="webgl-utils.js"></script>
    <script type="text/javascript" src="initShaders.js"></script>
    <script type="text/javascript" src="MV.js"></script>
    <script type="text/javascript" src="assignment2.js"></script>
</body>
</html>
